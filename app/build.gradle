apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
try {
  keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (IOException e) {}

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.0"

  defaultConfig {
    applicationId "com.gatunes.glcannon"
    minSdkVersion 14
    targetSdkVersion 26
    ndk {
      abiFilters 'armeabi-v7a'
    }
    externalNativeBuild {
      ndkBuild {
        arguments "NDK_DEBUG=0", "-j6"
      }
    }
  }

  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile keystoreProperties['storeFile'] ? rootProject.file(keystoreProperties['storeFile']) : keystoreProperties['storeFile']
      storePassword keystoreProperties['storePassword']
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
    }
  }
  externalNativeBuild {
    ndkBuild {
      path 'src/main/cpp/Android.mk'
    }
  }
}

task runDebug (type: Exec, dependsOn: 'installDebug') {
  commandLine android.getAdbExe().toString(), "shell",
  "monkey",
  "-p", "com.gatunes.glcannon",
  "-c", "android.intent.category.LAUNCHER", "1"
}
